plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.example.sampleapp"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        multiDexEnabled true

        testInstrumentationRunner "com.example.largetests.CustomTestRunner"
    }

    flavorDimensions rootProject.ext.commonFlavorDimensions
    productFlavors rootProject.ext.commonProductFlavors
    productFlavors {
        basic {
            applicationId 'com.example.sampleapp'
        }
        premium {
            applicationId 'com.example.sampleapp.premium'
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        beta {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            matchingFallbacks = ['release']
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    lintOptions {
        checkReleaseBuilds false
    }
}

apply from: "$rootDir/gradle/android-hilt.gradle"
apply from: "$rootDir/gradle/android-test.gradle"

dependencies {

    implementation project(':libraries:shop')

    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.core:core-ktx:$ktx_version"
    implementation "androidx.multidex:multidex:$multidex_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    androidTestImplementation project(path: ':tests:custom_test_runner')
}

tasks.withType(JavaCompile) {
    configure(options) {
        debug = true
        listFiles = true
        verbose = true
        options.compilerArgs << '-verbose' << '-XprintProcessorInfo'
        // options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked' // examples
    }
}